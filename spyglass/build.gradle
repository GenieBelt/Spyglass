apply plugin: 'com.android.library'
apply plugin: 'idea'
apply plugin: 'org.robolectric'
apply plugin: 'maven'

apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    testOptions.unitTests.all {
        // configure the set of classes for JUnit tests
        include '**/*Test.class'
        exclude '**/espresso/**/*.class'

        // configure max heap size of the test JVM
        maxHeapSize = '2048m'

        // configure the test JVM arguments
        jvmArgs '-XX:MaxPermSize=512m', '-XX:-UseSplitVerifier'

        // Specify max number of processes (default is 1)
        maxParallelForks = 4

        // Specify max number of test classes to execute in a test process
        // before restarting the process (default is unlimited)
        forkEvery = 150

        // configure whether failing tests should fail the build
        ignoreFailures false

        // use afterTest to listen to the test execution results
        afterTest { descriptor, result ->
            println "Executing test for ${descriptor.name} with result: ${result.resultType}"
        }
    }
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId GROUP
            version = VERSION_NAME
            artifactId project.getName()

            // Tell maven to prepare the generated "*.aar" file for publishing
            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
        }
    }
}

artifactory {
    contextUrl = artifactory_url
    publish {
        defaults {
            // Tell the Artifactory Plugin which artifacts should be published to Artifactory.
            publications('aar')
            publishArtifacts = true

            // Publish generated POM files to Artifactory (true by default)
            publishPom = true
        }
        repository {
            // The Artifactory repository key to publish to
            repoKey = 'libs-release-local'

            username = artifactory_user
            password = artifactory_password
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:23.0.1'
    compile 'com.android.support:support-annotations:23.0.'
    compile 'com.android.support:appcompat-v7:23.3.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'org.robolectric:robolectric:2.4'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

apply from: 'gradle-mvn-push.gradle'
